plugins {
    id("org.jetbrains.kotlin.plugin.compose") version "2.1.0"
}

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 35
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/INDEX.LIST']
        }
    }


    defaultConfig {
        applicationId "com.appy.widgets"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 13
        versionName "1.13"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++1z"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    task pythonPackage(description: 'Pack the appy package') {
        doLast {
            delete fileTree('/src/main/python/appy/dist') {
            }
            exec {
                workingDir = '/src/main/python/appy'
                commandLine = ['python', 'setup.py', 'sdist', '--formats=gztar']
            }
            copy {
                from '/src/main/python/appy/dist'
                into 'src/main/assets'
                rename 'appy.*\\.tar\\.gz', 'appy.targz'
            }
            copy {
                from '/src/main/python/logcat.py'
                into 'src/main/assets'
            }
            copy {
                from '/src/main/python/main.py'
                into 'src/main/assets'
            }
        }
    }

    task pythonExamples(type: Tar, description: 'Pack the appy examples') {
        from '/src/main/python/examples'
        archiveFileName = 'examples.targz'
        destinationDirectory = file('src/main/assets')
        compression = Compression.GZIP
    }

    task pythonTask {
        dependsOn 'pythonPackage'
        dependsOn 'pythonExamples'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn pythonTask
    }

    tasks.configureEach {if (name.startsWith("mergeArm")) { dependsOn("pythonTask") }}

    // Encapsulates your external native build configurations.
    externalNativeBuild {

        // Encapsulates your CMake build configurations.
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/cpp/exe'
        }
    }

    flavorDimensions "abi"

    productFlavors {
        arm64 {
            dimension "abi"
            ndk {
                abiFilter "arm64-v8a"
                versionCode = android.defaultConfig.versionCode
            }
        }
    }
    ndkVersion '21.0.6113669'
    namespace 'com.appy'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures.compose = true

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.8.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.2'
    implementation 'androidx.core:core-animation:1.0.0'
    implementation 'org.kamranzafar:jtar:2.3'
    implementation 'sh.calvin.reorderable:reorderable:2.4.3'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.compose.foundation:foundation-android:1.8.0-beta03'
    implementation 'androidx.compose.material3:material3-android:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.1.0'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:+'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
