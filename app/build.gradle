apply plugin: 'com.android.application'

import com.android.build.OutputFile

android {
    compileSdkVersion 29

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
    }

    defaultConfig {
        applicationId "com.appy.app"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 13
        versionName "1.13"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++1z"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    task pythonPackage(description: 'Pack the appy package') {
        doLast {
            delete fileTree('/src/main/python/appy/dist') {
            }
            exec {
                workingDir = '/src/main/python/appy'
                commandLine = ['python', 'setup.py', 'sdist', '--formats=gztar']
            }
            copy {
                from '/src/main/python/appy/dist'
                into 'src/main/assets'
                rename 'appy.*\\.tar\\.gz', 'appy.targz'
            }
            copy {
                from '/src/main/python/logcat.py'
                into 'src/main/assets'
            }
            copy {
                from '/src/main/python/main.py'
                into 'src/main/assets'
            }
        }
    }

    task pythonExamples(type: Tar, description: 'Pack the appy examples') {
        from '/src/main/python/examples'
        archiveName = 'examples.targz'
        destinationDir = file('src/main/assets')
        compression = Compression.GZIP
    }

    task pythonTask {
        dependsOn 'pythonPackage'
        dependsOn 'pythonExamples'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn pythonTask
    }

    // Encapsulates your external native build configurations.
    externalNativeBuild {

        // Encapsulates your CMake build configurations.
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/cpp/exe'
        }
    }

    flavorDimensions "abi"

    productFlavors {
        arm {
            dimension "abi"
            ndk {
                abiFilter "armeabi-v7a"
                versionCode = 0     + android.defaultConfig.versionCode
            }
        }
        arm64 {
            dimension "abi"
            ndk {
                abiFilter "arm64-v8a"
                versionCode = 10000 + android.defaultConfig.versionCode
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.0-beta1'
    implementation 'org.kamranzafar:jtar:2.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
